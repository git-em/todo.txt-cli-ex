#!/bin/bash

awk --re-interval -v date=$1 '
function getThreshold(date) {
    if (date == "") {
        return mktime(strftime("%Y %m %d 00 00 00", systime()))
    } else {
        return mktime(gensub(/([0-9]{4})-([0-9]{2})-([0-9]{2})/, "\\1 \\2 \\3 00 00 00", "", date))
    }
}
BEGIN {
    threshold = getThreshold(date)
}
/^x / {
    # Do not process done tasks. 
    print ""
    next
}
/^X / {
    # Explicitly trashed task. 
    print
    next
}
/(^| )x:[0-9]{4}-[0-9]{2}-[0-9]{2}/ {
    date = mktime(gensub(/^(.* )?x:([0-9]{4})-([0-9]{2})-([0-9]{2})([^0-9].*)?$/, "\\1 \\2 \\3 00 00 00", ""))
    if (date <= threshold) {
        print
        next
    }
}
/(^| )z:[0-9]{4}-[0-9]{2}-[0-9]{2}/ {
    date = mktime(gensub(/^(.* )?z:([0-9]{4})-([0-9]{2})-([0-9]{2})([^0-9].*)?$/, "\\1 \\2 \\3 00 00 00", ""))
    # do-until-then-trash tasks only get trashed on the day _after_ the do-until date. 
    if (date < threshold) {
        print
        next
    }
}
{
    print ""
}
'
