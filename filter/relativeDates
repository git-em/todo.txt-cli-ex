#!/bin/bash
awk --re-interval '
function relativeDate(date,      secondsSpan, daySpan, isInFuture) {
    if (date > threshold) {
        secondsSpan = date - threshold
        isInFuture = 1
    } else {
        secondsSpan = threshold - date
        isInFuture = 0
    }
    daySpan = int(secondsSpan / 86400)

    if (daySpan == 0) return "today"
    else if (daySpan == 1) return (isInFuture ? "tomorrow" : "yesterday")
    else if (daySpan == 7) return "(" (isInFuture ? "in " : "") "one week" (isInFuture ? "" : " ago") ")"
    else if (daySpan == 14) return "(" (isInFuture ? "in " : "") "two weeks" (isInFuture ? "" : " ago") ")"
    else if (daySpan < 14) return "(" (isInFuture ? "in " : "") daySpan " days" (isInFuture ? "" : " ago") ")"
    else return ""
}
BEGIN {
    threshold = mktime(strftime("%Y %m %d 00 00 00", systime()))
}
{
    for (i = 1; i <= NF; i++) {
        dateString = gensub(/^([[:alpha:]]:)?([0-9]{4})-([0-9]{2})-([0-9]{2}).*$/, "\\2 \\3 \\4 00 00 00", "", $i)
        if (dateString == $i) continue
        newDate = relativeDate(mktime(dateString))
        if (newDate != "")
            $i = gensub(/^([[:alpha:]]:)?([0-9]{4})-([0-9]{2})-([0-9]{2})/, "\\1" newDate, "", $i)
    }
    print
}
'
