#!/bin/bash
export TODOTXT_FILTER_LATEST_DUE_DAYSPAN=${TODOTXT_FILTER_LATEST_DUE_DAYSPAN:-3}

# Attention: This filter is to be used in post_filter_command, before formatting
# and coloring of priorities. It won't work as a TODOTXT_FINAL_FILTER! 
awk --re-interval -v date=$1 '
function getThreshold(date) {
    if (date == "") {
        return mktime(strftime("%Y %m %d 00 00 00", systime()))
    } else {
        return mktime(gensub(/([0-9]{4})-([0-9]{2})-([0-9]{2})/, "\\1 \\2 \\3 00 00 00", "", date))
    }
}
BEGIN {
    threshold = getThreshold(date)
    span = 86400 * ENVIRON["TODOTXT_FILTER_LATEST_DUE_DAYSPAN"]
}
/^ *[0-9]+ (x |\([A-Z]\) )| t:[0-9]{4}-[0-9]{2}-[0-9]{2}/ {
    # Do not process done and prioritized tasks, and tasks that have a due date. 
    print
    next
}
/ T:[0-9]{4}-[0-9]{2}-[0-9]{2}/ {
    date = mktime(gensub(/^.* T:([0-9]{4})-([0-9]{2})-([0-9]{2})([^0-9].*)?$/, "\\1 \\2 \\3 00 00 00", ""))
    if (date - span > threshold) {
        next
    }
}
{ print }
'
